apply plugin: "groovy"
apply plugin: "eclipse"
apply plugin: "maven"
apply plugin: "signing"

group = "com.github.marceloemanoel"
archivesBaseName="gradle-git-repository-plugin"
version="0.1"

if(!hasProperty("sonatypeUsername")){
    sonatypeUsername = ""
}

if(!hasProperty("sonatypePassword")){
    sonatypePassword = ""
}

signing {
    sign configurations.archives
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    groovy localGroovy()
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}
 
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
 
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
 
            pom.project {
               name 'gradle-git-repository-plugin'
               packaging 'jar'
               description 'gradle-git-repository-plugin adds the possibility to use git projects as project dependencies'
               url 'https://github.com/marceloemanoel/gradle-git-repository-plugin'
 
               scm {
                   url 'git@github.com:marceloemanoel/gradle-git-repository-plugin.git'
                   connection 'scm:git@github.com:marceloemanoel/gradle-git-repository-plugin.git'
                   developerConnection 'scm:git@github.com:marceloemanoel/gradle-git-repository-plugin.git'
               }
 
               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }
 
               developers {
                   developer {
                       id 'marceloemanoel'
                       name 'Marcelo Emanoel B. Diniz'
                   }
               }
           }
        }
    }
}

artifacts {
    archives jar
    archives groovydocJar
    archives sourcesJar
}
